#Область ПрограммныйИнтерфейс
 
Процедура ДополнитьФорму(Форма) Экспорт 

//{{ Стрельцов Д.А.: Дополнение формы документов реквизитами Контактное лицо, согласованная скидка и командой пересчитать таблицы товаров и услуг 
	ИмяФормы = Форма.ИмяФормы;
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьКомандуПересчитатьТоварыИУслуги(Форма);
		ДобавитьГруппуДляСкидкиВКонцеГруппаШапкаЛево(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;
//}}
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
 
Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма) Экспорт 
//{{ Стрельцов Д.А.: Дополнение контактным лицом формы документов заказ покупателя, поступление и реализация
	ПолеВвода =Форма.Элементы.Добавить("КонтактноеЛицо",Тип("ПолеФормы"),Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ИЗМ_КонтактноеЛицо";
//}}
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма) Экспорт 
//{{ Стрельцов Д.А.: Дополнение контактным лицом формы документов Оплата от покупателя и Оплата поставщику
	ПолеВвода =Форма.Элементы.Вставить("КонтактноеЛицо",Тип("ПолеФормы"), ,Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ИЗМ_КонтактноеЛицо";
//}}
КонецПроцедуры

Процедура ДобавитьГруппуДляСкидкиВКонцеГруппаШапкаЛево(Форма) Экспорт 
//{{ Стрельцов Д.А.: Дополнение группой, содержащей согласованную скидку и кнопку перерасчета, форму документа заказ покупателя
	ГруппаДляСкидки = Форма.Элементы.Добавить("ГруппаСкидка",Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаДляСкидки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаДляСкидки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ГруппаДляСкидки.ОтображатьЗаголовок = Ложь;
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка",Тип("ПолеФормы"), ГруппаДляСкидки);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ИЗМ_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");
	КнопкаПерерасчета = Форма.Элементы.Добавить("КнопкаПересчитатьТоварыИУслуги", Тип("КнопкаФормы"), ГруппаДляСкидки);
	КнопкаПерерасчета.ИмяКоманды = "ПересчитатьТоварыИУслуги";
//}}
КонецПроцедуры

Процедура ДобавитьКомандуПересчитатьТоварыИУслуги(Форма) Экспорт 
//{{ Стрельцов Д.А.: Добавление команды перерасчета
	Команда = Форма.Команды.Добавить ("ПересчитатьТоварыИУслуги");
	Команда.Заголовок = "Пересчитать товары и услуги";
	Команда.Действие = "КомандаПересчитатьТоварыИУслуги";
//}}
КонецПроцедуры

#КонецОбласти